apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'

repositories {
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.lovejoy777.showcase"
        minSdkVersion 21
        targetSdkVersion 22
        versionCode 17
        versionName "1.0"
        archivesBaseName = "Showcase"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'LICENSE.txt'
    }

    play {
        serviceAccountEmail = System.getenv("CART_EMAIL")
        pk12File = file(System.getenv("CERT_LOCATION") == null ? 'cert.cert' : System.getenv("CERT_LOCATION"))
        track = System.getenv("TRACK") == null ? 'alpha' : System.getenv("TRACK")
    }

    signingConfigs {
        release {
            storeFile file(System.getenv("KEY_LOCATION") == null ? 'key.key' : System.getenv("KEY_LOCATION"))
            storePassword System.getenv("KEY_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_ALIAS_PASSWORD")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles "proguard-rules.pro"
            proguardFiles getDefaultProguardFile('proguard-android.txt')
            signingConfig signingConfigs.release
        }
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:cardview-v7:22.2.0'
    compile 'com.android.support:recyclerview-v7:22.2.0'
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:palette-v7:22.2.0'
    compile 'com.android.support:design:22.2.0'
    compile 'com.fasterxml.jackson.core:jackson-core:2.4.1'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.4.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.quinny898.library.persistentsearch:library:1.0.0-SNAPSHOT'
    compile 'com.google.guava:guava:18.0'

    androidTestCompile 'com.android.support.test:runner:0.3'
    androidTestCompile 'com.android.support.test:rules:0.3'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2'
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.1'
    androidTestCompile 'com.jayway.android.robotium:robotium-solo:5.4.1'
}
